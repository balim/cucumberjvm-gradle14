apply plugin: 'java'

ext.versions = [
        cucumberJvm: "1.0.11",
        spring: "3.1.1.RELEASE",
        logback: "1.0.9",
        slf4j: "1.7.2",
        junit: "4.10"
]

repositories {
    mavenCentral()
}

dependencies {
    compile "ch.qos.logback:logback-classic:${versions.logback}"
    compile "org.slf4j:log4j-over-slf4j:${versions.slf4j}"
    testCompile "info.cukes:cucumber-java:${versions.cucumberJvm}"
    testCompile "info.cukes:cucumber-spring:${versions.cucumberJvm}"
    testCompile "info.cukes:cucumber-junit:${versions.cucumberJvm}"
    testCompile "org.springframework:spring-beans:${versions.spring}"
    testCompile "org.springframework:spring-context:${versions.spring}"
    testCompile "org.springframework:spring-tx:${versions.spring}"
    testCompile group: "junit", name: "junit", version: versions.junit
}


sourceSets {
    intTest {
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + sourceSets.main.output + configurations.testRuntime
    }
}

task intTest(type: Test) {
    testClassesDir = sourceSets.intTest.output.classesDir
    classpath = sourceSets.intTest.runtimeClasspath

    def defaultTag = "~@IgnoreThisHighlyUnlikelyTagName_0987654321f0464ffeb7327746e9065b1823c377f4"
    def tags = System.getProperty("tags", defaultTag)
    def cucumberOptions = "--tags ${tags} --format pretty --monochrome --glue classpath:com/example/cuke/steps classpath:com/example/cuke/steps"
    systemProperty 'cucumber.options', cucumberOptions
    testLogging {
        showStandardStreams = true
        exceptionFormat "full"
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
    }
    outputs.upToDateWhen { false }

}

task wrapper(type: Wrapper) {
    gradleVersion = '1.5'
}

tasks.withType(Compile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties = System.getProperties()
    testLogging.showStandardStreams = true
}
